---
title: "SSM職業小分類（追加コード含む）をSSM職業8分類に変換するStataシンタックス"
author: "Ryota Mugiyama"
date: "2016-04-07"
categories: 
  - "Quantitative methods"
  - "Social stratification"
image: ../images/website.jpg
editor: 
  markdown: 
    wrap: 72
---

（2017/5/11 701番以降の追加コードを修正） 

（2024/11/22 691, 805, 806を追加）

（2024/11/22 「管理職戻し」の説明を追加）

標題の通りです。ご使用は自己責任でお願いします。作成に際しては、1995年SSM調査研究会，1995，『SSM産業分類・職業分類(95年版)』を参照しました。

ミスや不適切な箇所を見つけた方は、ご指摘くださいますようお願いします。

SPSSを使う場合は[村瀬洋一先生のHP](https://www2.rikkyo.ac.jp/web/murase/ssm/job.html)が、Rを使う場合は[寺沢拓敬先生のHP](https://terasawat.hatenablog.jp/entry/20090607/1244363041)が、それぞれ参考になります。

``` stata
*** recode occupation into SSM 8 classification (including no-job) for stata **
*** 20241122 made by Ryota Mugiyama *******************************************

/******************************************************************
SSM職業小分類をSSM職業8分類に変換するシンタックス（Stata用）になります。
ただし、701番以降の追加コードは以下に示す通り麦山が独自に分類を割り当てています。
使用は自己責任でお願いします。
シンタックスは、recode関数を用いてSSM職業小分類（occupation）から
SSM職業8分類（ssmocc）を作成するというかたちになっています。
行は見やすさのため、1行ごとに1への割り当て、2への割り当て…というふうに分けています。
9行目は追加コードの分類、10行目は分類不能や非該当への分類を示しています。
 ********************************************************************/

/***SSM1995年版職業分類に含まれないコードの8分類への割り当て****************
701"スーパーなどのレジスター係員" → 4"販売"
702"大工" → 5"熟練"
703"教員" → 1"専門"
704"製品製造作業者" → 6"半熟練"
706"宅配便の配達" → 7"非熟練"
801"介護員・ヘルパー" → 6"半熟練"
802"その他の医療・福祉サービス職従事者" → 6"半熟練"
803"雇われている販売店長" → 4"販売"
804"雇われている飲食店長" → 4"販売"
805"ファイナンシャルプランナー、社会保険労務士など" → 1"専門"
806"歯科助手、看護助手" → 6"半熟練"
********************************************************************/
recode occupation ///
(501/544 = 1)(609/610 = 1)(615 = 1) ///
(545/553 = 2)(608 = 2) ///
(554/565 = 3)(586 = 3)(590 = 3)(593/598 = 3)(616/619 = 3) ///
(566/577 = 4)(582/585 = 4)(587/589 = 4) ///
(579 = 5)(581 = 5)(623/624 = 5)(626 = 5)(628 = 5)(631 = 5)(633 = 5)(635/644 = 5)(647 = 5)(651 = 5)(654/656 = 5)(658 = 5)(660/666= 5)(668 = 5)(670/675 = 5)(677/681 = 5)(684 = 5) ///
(580 = 6)(606/607 = 6)(611/614 = 6)(625 = 6)(627 = 6)(629/630 = 6)(632 = 6)(634 = 6)(645/646 = 6)(648/650 = 6)(652/653 = 6)(657 = 6)(659 = 6)(667 = 6)(669 = 6)(672 = 6)(676 = 6) ///
(578 = 7)(591/592 = 7)(620/622 = 7)(682/683 = 7)(685/688 = 7) ///
(599/605 = 8) ///
(701 = 4)(702 = 5)(703 = 1)(704 = 6)(706 = 7) ///
(801/802 = 6)(803/804 = 4)(805 = 1)(806 = 6) ///
(689 =.)(691 = .)(705 =.)(707 =.)(987 =.)(998 = .)(999 =.) ///
, gen(ssmocc)

/*値ラベルを振りたい場合は以下を追加してください。*/
lab def occ 1"専門" 2"管理" 3"事務" 4"販売" 5"熟練" 6"半熟練" 7"非熟練" 8"農林"
lab val ssmocc occ

/*「無職」を職業カテゴリに含めたい場合は以下を追加してください。*/
replace ssmocc = 9 if occupation == 998
```

なお、上記コードでは職業の情報のみを用いて8分類を作成していますが、管理職については、もともとの記載内容を残しておくという目的で、管理職とみなせるような場合でも、管理職ではない職業小分類コードが振られている可能性があります。具体的には、2015年社会階層と社会移動調査研究会，2018，『2015年SSM調査コードブック』2015年社会階層と社会移動調査研究会，p.80に次のような記述があります。

> (2)「管理職」のコードについて
> （以下は原則であり、仕事の内容が管理的としか言えないときは、管理的職業にコードする。ただし、ここでいう「管理」とは部下など人の管理であって、在庫管理や物品管理などは含まないことに注意。）
>
> ・従業上の地位が「1 経営者・役員」「7 自営業主・自由業者」の場合
>
> 規模5人未満　必ず、管理的職業以外の仕事の内容でコードする。
>
> 規模30人未満　管理的職業以外の仕事の内容を優先してコードする。
>
> 規模30人以上　原則としていずれか該当する管理的職業でコード（「548
> 会社役員」「549
> その他の法人・団体の役員」など）するが、それ以外の仕事の内容が書いてあれば、それに従ってコードする。
>
> ・従業上の地位が「2 常時雇用されている一般従業者」「3 パート・アルバイト」「4 派遣社員」「5 契約社員」「6 臨時雇用」「8 家族従業者」の場合
>
> 役職が「4課長」以上のとき・・・
>
> 規模5人未満　必ず、管理的職業以外の仕事の内容でコードする。
>
> 規模30人未満　管理的職業以外の仕事の内容を優先してコードする。
>
> 規模30人以上　原則としていずれか該当する管理的職業でコード（「550
> 会社・団体等の管理職員」など）するが、それ以外の仕事の内容が書いてあれば、それに従ってコードする。
>
> 役職が「3係長」以下のとき・・・
>
> 必ず、管理的職業以外の仕事の内容でコードする。
>
> ・専門的管理職（設計技師長、病院長、学校長など）は、「専門」の方を優先する。

つまり、従業員数30人以上の企業に勤める課長以上であったり、経営者であったとしても、管理職以外のコードが振られているケースがあるということです。もし、これらの人を管理職だとみなすのであれば、上記に加えてさらに次のコードを実行する必要があります。

``` stata
*** 管理職になるべきだが管理職にコードされていない人を管理職に戻す
*** 企業規模30人以上で、従業上の地位が経営者・役員もしくは自営業主・自由業者の場合、管理職とする。ただし専門職はそのようにしない
replace ssmocc = 2 if (status == 1 | status == 7) & (firmsize >= 5 & firmsize <= 10) & ssmocc != 1	
*** 企業規模30人以上で、従業上の地位が正規雇用、非正規雇用、家族従業者で、役職が課長以上の場合、管理職とする。ただし専門職はそのようにしない
replace ssmocc = 2 if (status >= 2 & status <= 6 | status == 8) & (firmsize >= 5 & firmsize <= 10) & (title >= 4 & title <= 6) & ssmocc != 1
```

ただしstatus, firmsize, titleはそれぞれ次のような値が入った変数とします（SSM2015に準拠）。

- status: 従業上の地位（1: 経営者・役員、2: 常時雇用されている一般従業者、3: パート・アルバイト、4: 派遣社員、5: 契約社員・嘱託、6: 臨時雇用、7: 自営業主・自由業者、8: 家族従業者）
- firmsize: 従業員数（1: 1人、2: 2-4人、3: 5-9人、4: 10-29人、5: 30-99人、6: 100-299人、7: 300-499人、8: 500-999人、9: 1000人以上、10: 官公庁）
- title: 役職（1: 役職なし、2: 監督・職長・班長・組長、3: 係長・係長相当職、4: 課長・課長相当職、5: 部長・部長相当職、6: 社長・重役・役員・理事）
